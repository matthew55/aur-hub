# Maintainer: Matthew A. <matthewa@duck.com>
# Contributor: Daniel Leining <daniel@the-beach.co>

pkgname=chunky
pkgver=2.4.4
pkgrel=1
pkgdesc="Minecraft mapping and rendering tool"
url="http://chunky.llbit.se/"
arch=('any')
license=('GPL3')
depends=('java-runtime=17' 'java-openjfx=17')
makedepends=('gendesk')
source=(ChunkyLauncher.jar::https://chunkyupdate.lemaik.de/ChunkyLauncher.jar
       chunky.sh)
sha512sums=('e0121b370373b74e96b3bbc7901341f519f0eea796be965343dda0524b68c986da92cd91e5641c67e76f1cac2b45b0e18ed8f3b731384961be727e0d1656b830' 
			'7ce6f5937a1f844ddbc1db8fa1c219e2816cf244c56b68214f43a5d994198d3ff5cad2a844b1dda1aaa99984ec1adc4a9c8eb085dc3f8da6ce6c51bbe5bde4bd')

build() {
  gendesk -f -n

  bsdtar -xf ChunkyLauncher.jar "*chunky-cfg.png"
  find . -name "chunky-cfg.png" -exec mv {} . \;
}

package() {
  install -Dm755 chunky.sh "$pkgdir/usr/bin/chunky"
  install -Dm644 ChunkyLauncher.jar "$pkgdir/usr/share/java/chunky/ChunkyLauncher.jar"
  install -Dm644 chunky.desktop "$pkgdir/usr/share/applications/chunky.desktop"
  install -Dm644 chunky-cfg.png "$pkgdir/usr/share/pixmaps/chunky.png"
}

